Sub InsertWeatherSlide()
    Dim xml As Object
    Dim url As String
    Dim stationID As String
    stationID = "KBDL" ' <-- Change this to another station ID if needed
    url = "https://forecast.weather.gov/xml/current_obs/" & stationID & ".xml"

    ' Download and load XML
    Set xml = CreateObject("MSXML2.DOMDocument.6.0")
    xml.async = False
    xml.Load url

    If xml.ParseError.ErrorCode <> 0 Then
        MsgBox "XML load error"
        Exit Sub
    End If

    ' Extract data
    Dim location As String: location = GetXmlValue(xml, "location")
    Dim temperature As String: temperature = GetXmlValue(xml, "temperature_string")
    Dim weather As String: weather = GetXmlValue(xml, "weather")
    Dim humidity As String: humidity = GetXmlValue(xml, "relative_humidity") & "%"
    Dim wind As String: wind = GetXmlValue(xml, "wind_string")
    Dim obsTime As String: obsTime = GetXmlValue(xml, "observation_time")

    ' Update slide and populate with weather data
    Dim sld As Slide
    ' Updates (in this case) slide 1 (potentially necessary todo based on customer slideshow)
    Set sld = ActivePresentation.Slides(1)

    ' Set text on existing placeholders or text boxes
    sld.Shapes(1).TextFrame.TextRange.Text = "Weather for " & location
    sld.Shapes(2).TextFrame.TextRange.Text = _
    "Observation Time: " & obsTime & vbCrLf & _
    "Temperature: " & temperature & vbCrLf & _
    "Weather: " & weather & vbCrLf & _
    "Humidity: " & humidity & vbCrLf & _
    "Wind: " & wind
End Sub

Function GetXmlValue(xml As Object, tagName As String) As String
    On Error Resume Next
    GetXmlValue = xml.SelectSingleNode("//" & tagName).Text
    If GetXmlValue = "" Then GetXmlValue = "N/A"
End Function


